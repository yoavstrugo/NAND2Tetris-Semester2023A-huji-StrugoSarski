// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:
    Mux16(a=instruction, b=ALUOut, sel=instruction[15], out=ARegisterIn);

    Not(in=instruction[15], out=isAInstruction);
    
    Or(a=instruction[5], b=isAInstruction, out=doLoadA);
    
    ARegister(in=ARegisterIn, load=doLoadA, out=ARegisterOut); // Register A

    Mux16(a=ARegisterOut, b=inM, sel=instruction[12], out=RegisterAorM);

    ALU(y=RegisterAorM, x=DRegisterOut, 
        zx=instruction[11], 
        nx=instruction[10], 
        zy=instruction[9], 
        ny=instruction[8], 
        f=instruction[7], 
        no=instruction[6], 
        out=ALUOut, zr=ALUzr, ng=ALUng);

    // Only load D if it's a C-operation (=instruction[15])
    And(a=instruction[4], b=instruction[15], out=doLoadD);
    DRegister(in=ALUOut, load=doLoadD, out=DRegisterOut); // Register D

    // WriteM Output
    // If C-operation and dst is M
    And(a=instruction[15], b=instruction[3], out=writeM); 

    Or16(a=ARegisterOut, b=ARegisterOut, out[0..14]=addressM); // AddressM Output

    Or16(a=ALUOut, b=ALUOut, out=outM); // OutM
    
    g(j1= instruction[2], j2= instruction[1], j3= instruction[0], zr= ALUzr, ng= ALUng, out= pcLoadCondition);
    And(a=pcLoadCondition, b=instruction[15], out=pcLoad); // Condition is met and it's a C operation (meaning a jump is possible)
    
    PC(in=ARegisterOut, load=pcLoad, inc=true, reset=reset, out[0..14]=pc);
        
}